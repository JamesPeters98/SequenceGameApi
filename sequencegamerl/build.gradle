plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.11.4'
}

group = 'com.jamesdpeters'
version = '0.0.1-SNAPSHOT'
description = 'sequencegamerl'

def osName = System.getProperty('os.name').toLowerCase()
def osArch = System.getProperty('os.arch').toLowerCase()
def nd4jNativeClassifier

if (osName.contains('mac')) {
    nd4jNativeClassifier = (osArch.contains('aarch64') || osArch.contains('arm64')) ? 'macosx-arm64' : 'macosx-x86_64'
} else if (osName.contains('win')) {
    nd4jNativeClassifier = 'windows-x86_64'
} else if (osName.contains('linux')) {
    nd4jNativeClassifier = (osArch.contains('aarch64') || osArch.contains('arm64')) ? 'linux-arm64' : 'linux-x86_64'
} else {
    throw new GradleException("Unsupported OS for ND4J natives: os.name='${osName}', os.arch='${osArch}'")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'io.netty') {
                details.useVersion '4.1.48.Final'
                details.because 'DL4J UI uses Vert.x 3.9.0 which requires Netty 4.1.x'
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.deeplearning4j:deeplearning4j-core:1.0.0-M2.1'
    implementation 'org.deeplearning4j:deeplearning4j-ui:1.0.0-M2.1'
    implementation 'org.nd4j:nd4j-native:1.0.0-M2.1'
    runtimeOnly "org.nd4j:nd4j-native:1.0.0-M2.1:${nd4jNativeClassifier}"
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
